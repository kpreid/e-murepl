pragma.syntax("0.9")

def resourcePath(lookupName) {
  return <resource>[`org/switchb/e/murepl/vat/$lookupName`].toURI().getPath()
}

def makeIsolatedVatAuthor(<unsafe>, jRuntime, introducer, identityMgr, stdout, stderr, seedVat) {
  def limitPath := resourcePath("limit.sh")
  def System := <unsafe:java.lang.makeSystem>
  def makeOutputStreamWriter := <unsafe:java.io.makeOutputStreamWriter>
  
  return def makeIsolatedVat {
    to run() {
      
      # A promise for the remote vat's internal controller
      def [remoteRcvr, remoteResolver] := Ref.promise()
      
      # SturdyRef to the resolver for bootstrapping communication
      def [bootstrapSR, bootstrapCancel, _] :=
        identityMgr.makeKnown(remoteResolver)
      def text := introducer.sturdyToURI(bootstrapSR)
      
      # Start actual subprocess
      def process := <unsafe:java.lang.makeRuntime>.getRuntime().exec(
        [limitPath, text],
        null,
        null)

      # Mainly for debugging...
      # raw* are guaranteed not to be the updoc output streams
      def rawStderr := makeOutputStreamWriter(System.getErr())
      def rawStdout := makeOutputStreamWriter(System.getOut())
      process.attachStderr(rawStderr)
      process.attachStdout(rawStdout)
      
      def [processExitCodeVow, tvat] := process.terminates()
      
      # Clean up upon process shutdown
      when (processExitCodeVow) -> {} catch _ {} finally {
        rawStderr.flush()
        rawStdout.flush()
        bootstrapCancel()
        tvat.orderlyShutdown()
        remoteResolver.resolveRace(
          if (Ref.isBroken(processExitCodeVow)) {
            processExitCodeVow
          } else {
            Ref.broken("Isolated vat process unexpectedly terminated")
          })
        # process.abort()
      }
      
      # Revoke bootstrap sturdyref as soon as it has been used
      when (remoteRcvr) -> {} catch _ {} finally {
        bootstrapCancel()
      }
              
      def outerController {
        to kill() {
          process.destroy()
        }
        to terminates() {
          return processExitCodeVow
        }
      }
      
      return [remoteRcvr, outerController]
    }
  }
}